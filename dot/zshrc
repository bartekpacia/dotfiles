#!/usr/bin/env zsh

# Run cross shell configuration
. ~/.shrc

export ZSH="$HOME/.oh-my-zsh" # Path to oh-my-zsh installation
export ZSH_CONFIG="$HOME/.zshrc"
export ZSH_CUSTOM="$ZSH/custom"
export ZSH_COMPDUMP="$ZSH_CACHE_DIR/.zcompdump-$SHORT_HOST-$ZSH_VERSION"
export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=8"

mkdir -p "$XDG_STATE_HOME/zsh" # zsh doesn't create it

HISTFILE="$XDG_STATE_HOME/zsh/history"
SAVEHIST=1000000
HISTSIZE=2000000
setopt EXTENDED_HISTORY       # Record timestamp in history
setopt HIST_EXPIRE_DUPS_FIRST # Expire duplicate entries first when trimming history
setopt HIST_IGNORE_DUPS       # Dont record an entry that was just recorded again
setopt HIST_IGNORE_ALL_DUPS   # Delete old recorded entry if new entry is a duplicate
setopt HIST_FIND_NO_DUPS      # Do not display a line previously found
setopt HIST_IGNORE_SPACE      # Dont record an entry starting with a space
setopt HIST_SAVE_NO_DUPS      # Dont write duplicate entries in the history file
setopt SHARE_HISTORY          # Share history between all sessions
setopt HIST_VERIFY            # Do not execute commands using history (e.g.: using !$) immediately
setopt MENU_COMPLETE          # Auto-insert first completion (see https://unix.stackexchange.com/q/12288/417321)
unsetopt BEEP                 # Disable the annoying beep

ZSH_THEME="mlh"
MLH_AT_SYMBOL=" at "
MLH_IN_SYMBOL=" in "
MLH_ON_SYMBOL=" on "
MLH_SHELL_SYMBOL="$ "

CASE_SENSITIVE="true" # ohmyzsh: use case-sensitive completion

plugins=(
	1password
	# flutter
	`#fzf` $([ -x "$(command -v fzf)" ] && echo 'fzf-tab')
	`#fzf` $([ -x "$(command -v fzf)" ] && echo 'fzf')
	macos
	swiftpm
	terraform # TODO: submit to upstream: https://github.com/hashicorp/homebrew-tap/blob/master/Formula/terraform.rb
	zsh-autosuggestions
	zsh-syntax-highlighting
)

if command -v brew >/dev/null 2>&1; then
	# Set up completions for `brew` command and `brew`-installed programs
	# See: https://docs.brew.sh/Shell-Completion#configuring-completions-in-zsh
	FPATH="$(brew --prefix)/share/zsh/site-functions:$FPATH"

	# Set up zsh-completions
	FPATH="$(brew --prefix)/share/zsh-completions:$FPATH"
fi

if [ -d "$ZSH" ]; then
	source "$ZSH/oh-my-zsh.sh"
	# oh-my-zsh initializes completion, so shrc needs to be sourced before it.
	# oh-my-zsh sets aliases, so aliases needs to be sourced after it.
	FPATH="$ZSH_CACHE_DIR/completions:$FPATH"
fi

# make CMD+Delete delete line before the cursor (not the whole line)
bindkey \^U backward-kill-line

NOEXPAND=(bat micro delta grep) # list of aliases to never expand

# expand aliases on 'space' key (adapted from https://www.reddit.com/r/zsh/comments/bbbluo/comment/ekhpxer)
expand-alias-space() {
    local word=${LBUFFER##* }  # get the word before the cursor
    if (( ${NOEXPAND[(Ie)$word]} )); then
        zle self-insert
        return
    fi

	zle _expand_alias
	zle self-insert
}
zle -N expand-alias-space-widget expand-alias-space # create a new widget which calls the 'expand-alias-space' function
bindkey ' ' expand-alias-space-widget         # call the widget when the space key is pressed

# expand aliases on 'enter' key
expand-alias-enter() {
    local word=${LBUFFER##* }  # get the word before the cursor
    if (( ${NOEXPAND[(Ie)$word]} )); then
        zle accept-line
        return
    fi
	zle _expand_alias
	zle accept-line
}
zle -N expand-alias-enter-widget expand-alias-enter # create a new widget which calls the 'expand-alias-enter' function
bindkey '^M' expand-alias-enter-widget              # call the widget when the enter key (^M) is pressed

# Load cross-shell aliases
source "$HOME/.dotfiles/script/aliases"

# Set up Atuin (https://atuin.sh)
if command -v atuin >/dev/null 2>&1; then
	eval "$(atuin init zsh --disable-up-arrow)"
fi

# Set up gcloud CLI
if command -v gcloud >/dev/null 2>&1; then
	. "$(brew --prefix)/share/google-cloud-sdk/path.zsh.inc"       # see https://github.com/orgs/Homebrew/discussions/6548
	. "$(brew --prefix)/share/google-cloud-sdk/completion.zsh.inc" # see https://github.com/orgs/Homebrew/discussions/6547
fi
